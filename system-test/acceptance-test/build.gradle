plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.optivem.eshop.systemtest'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation project(':test-infrastructure')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
}

tasks.named('test') {
    useJUnitPlatform()

    // Pass system properties from command line to test JVM
    systemProperty 'environment', System.getProperty('environment')
    systemProperty 'externalSystemMode', System.getProperty('externalSystemMode')

    // Enable parallel test execution at Gradle level
    maxParallelForks = Runtime.runtime.availableProcessors()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
        showExceptions = true
        showCauses = true
        showStackTraces = true
    }
}

// Disable bootJar task since this is a test-only module
tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
}

